plugins {
    id "com.jfrog.artifactory" version "4.4.0"
    id 'java'
    id 'maven-publish'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
        
     maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

def getVersion(){
    //Load version file
    def versionFile = file('version.properties')
    Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(versionFile))

    // Read and parse semvers
    def major = versionProps['MAJOR'].toInteger()
    def minor = versionProps['MINOR'].toInteger()
    def patch = versionProps['PATCH'].toInteger() + 1

    // Write semvers
    versionProps['MAJOR'] = major.toString()
    versionProps['MINOR'] = minor.toString()
    versionProps['PATCH'] = patch.toString()
    versionProps.store(versionFile.newWriter(), null)

    return "${major}.${minor}.${patch}"
}
def projectVersion = getVersion()

jar {
    manifest {
        attributes 'Implementation-Vendor': 'SAMsix LLC',
                   'Implementation-Title': 'SAMsix Custom Graphhopper',
                   'Implementation-Version': projectVersion
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId 'com.samsix'
            artifactId 'graphhopper'
            version projectVersion
        }
    }
}

artifactory {
    contextUrl = ARTIFACTORY_URL
    publish {
        repository {
            repoKey = 'libs-release-local'
            username = ARTIFACTORY_USER
            password = ARTIFACTORY_PASSWORD
        }
        defaults {
            publications ('mavenJava')
        }
    }
}

dependencies {
    compile group: 'net.sf.trove4j', name: 'trove4j', version:'3.0.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.7'
    compile group: 'org.apache.xmlgraphics', name: 'xmlgraphics-commons', version:'1.5'
    compile group: 'com.google.protobuf', name: 'protobuf-java', version:'2.6.0'
    compile group: 'org.openstreetmap.osmosis', name: 'osmosis-osm-binary', version:'0.43.1'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.7.7'
    testCompile group: 'log4j', name: 'log4j', version:'1.2.17'
    testCompile group: 'junit', name: 'junit', version:'4.11'
}
